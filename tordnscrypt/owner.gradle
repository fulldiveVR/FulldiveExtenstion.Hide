/*
 * This file is part of InviZible Pro.
 *     InviZible Pro is free software: you can redistribute it and/or modify
 *     it under the terms of the GNU General Public License as published by
 *     the Free Software Foundation, either version 3 of the License, or
 *     (at your option) any later version.
 *     InviZible Pro is distributed in the hope that it will be useful,
 *     but WITHOUT ANY WARRANTY; without even the implied warranty of
 *     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *     GNU General Public License for more details.
 *     You should have received a copy of the GNU General Public License
 *     along with InviZible Pro.  If not, see <http://www.gnu.org/licenses/>.
 *     Copyright 2019-2022 by Garmatin Oleksandr invizible.soft@gmail.com
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

def keystorePropertiesFile = file("/usr/local/KStore/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 30


    flavorDimensions "version", "processor"

    signingConfigs {
        stablesign {
            storeFile file("/usr/local/KStore/pan_alexander.jks")
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keySAlias']
            keyPassword keystoreProperties['keySPassword']
        }

        betasign {
            storeFile file("/usr/local/KStore/pan_alexander.jks")
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyBAlias']
            keyPassword keystoreProperties['keyBPassword']
        }
    }

    productFlavors {
        lite {
            applicationId "pan.alexander.tordnscrypt.stable"
            versionName "5.3.0"
            dimension = 'version'
            signingConfig signingConfigs.stablesign
            resValue 'string', 'package_name', applicationId
        }

        pro {
            applicationId "pan.alexander.tordnscrypt.stable"
            versionName "5.3.0"
            dimension = 'version'
            signingConfig signingConfigs.stablesign
            resValue 'string', 'package_name', applicationId
        }

        beta {
            applicationId "pan.alexander.tordnscrypt"
            versionName "1.3.3"
            dimension = 'version'
            signingConfig signingConfigs.betasign
            resValue 'string', 'package_name', applicationId
        }

        google_play {
            minSdkVersion 22
            applicationId "pan.alexander.tordnscrypt.gp"
            versionName "5.3.0"
            dimension = 'version'
            signingConfig signingConfigs.stablesign
            resValue 'string', 'package_name', applicationId
            resValue 'string', 'gp_property', keystoreProperties['gpPublicKey']
        }

        armv7a {
            dimension = 'processor'
            resValue 'string', 'appProcVersion', 'armv7a'
            versionCode = 2

            ndk {
                abiFilters 'armeabi-v7a'
            }
        }

        arm64 {
            dimension = 'processor'
            resValue 'string', 'appProcVersion', 'arm64'
            versionCode = 3

            ndk {
                abiFilters 'arm64-v8a'
            }
        }

    }

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 133

        resConfigs "en", "ru-rRU", "ru-rUA", "pl", "de", "fa", "fi", "in", "fr", "ja", "zh", "es", "pt-rBR"

        externalNativeBuild {
            cmake {
                cppFlags ""
                arguments "-DANDROID_PLATFORM=android-19"
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + System.currentTimeMillis() + "L)"

        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    buildToolsVersion = '30.0.3'

    bundle {
        language {
            enableSplit = false
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs 'libs/'
        }
    }

    packagingOptions{
        doNotStrip '*/*/libdnscrypt-proxy.so'
        doNotStrip '*/*/libobfs4proxy.so'
        doNotStrip '*/*/libsnowflake.so'
    }

    applicationVariants.all { variant ->

        def abiVersion = variant.mergedFlavor.versionCode

        variant.outputs.each { output ->
            output.versionCodeOverride = abiVersion * 1000 + defaultConfig.versionCode
        }
    }

    ndkVersion '21.4.7075529'

    buildFeatures {
        viewBinding true
    }

    kotlinOptions.useIR = true

}



repositories {
    mavenCentral()
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.recyclerview:recyclerview:1.2.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    google_playImplementation 'com.android.billingclient:billing:4.0.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    //noinspection GradleDynamicVersion
    implementation 'eu.chainfire:libsuperuser:1.1.0.+'
    implementation 'com.jrummyapps:android-shell:1.0.1'
    implementation "androidx.core:core-ktx:1.5.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation project(':filepicker')
}
