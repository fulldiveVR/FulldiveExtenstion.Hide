apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 30

    flavorDimensions "version", "processor"

    productFlavors {

        fdroid{
            applicationId "com.fulldive.extension.tordnscrypt"
            versionName "5.3.0"
            dimension = 'version'
            resValue 'string', 'package_name', applicationId
        }

        armv7a{
            dimension = 'processor'
            resValue 'string', 'appProcVersion', 'armv7a'
            versionCode = 2

            ndk {
                abiFilters 'armeabi-v7a'
            }
        }

        arm64{
            dimension = 'processor'
            resValue 'string', 'appProcVersion', 'arm64'
            versionCode = 3

            ndk {
                abiFilters 'arm64-v8a'
            }
        }

        universal{
            dimension = 'processor'
            resValue 'string', 'appProcVersion', 'universal'
            versionCode = 4

            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }
        }

    }

    signingConfigs {
        release {
            storeFile file('../keys/keys.jks')
            storePassword System.getenv("FULLDIVE_KEYSTORE_PASSWORD")
            keyAlias System.getenv("FULLDIVE_ALIAS")
            keyPassword System.getenv("FULLDIVE_ALIAS_PASSWORD")
        }
    }

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 133

        resConfigs "en", "ru-rRU", "ru-rUA", "pl", "de", "fa", "fi", "in", "fr", "ja", "zh", "es", "pt-rBR"

        externalNativeBuild {
            cmake {
                cppFlags ""
                arguments "-DANDROID_PLATFORM=android-19"
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + System.currentTimeMillis() + "L)"

        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    buildToolsVersion = '30.0.3'

    bundle {
        language {
            enableSplit = false
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs 'libs/'
        }
    }

    packagingOptions{
        doNotStrip '*/*/libdnscrypt-proxy.so'
        doNotStrip '*/*/libi2pd.so'
        doNotStrip '*/*/libobfs4proxy.so'
        doNotStrip '*/*/libsnowflake.so'
        doNotStrip '*/*/libtor.so'
    }

    applicationVariants.all { variant ->

        def abiVersion = variant.mergedFlavor.versionCode

        variant.outputs.each { output ->
            output.versionCodeOverride = abiVersion * 1000 + defaultConfig.versionCode
        }
    }

    ndkVersion '21.4.7075529'

    buildFeatures {
        viewBinding true
    }

    kotlinOptions.useIR = true

}

repositories {
    mavenCentral()
    maven { url "http://office.fulldive.com:8083" }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.recyclerview:recyclerview:1.2.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    //noinspection GradleDynamicVersion
    implementation 'eu.chainfire:libsuperuser:1.1.0.+'
    implementation 'com.jrummyapps:android-shell:1.0.1'
    implementation "androidx.core:core-ktx:1.5.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'com.github.angads25:filepicker:1.1.1'
    // Junk code
    if (System.getenv("GUARD_ENABLED")) {
        releaseImplementation "com.snore.guard:hide:1.3.0"
    }
}
