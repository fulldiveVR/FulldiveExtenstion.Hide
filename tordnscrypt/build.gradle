/*
 * This file is part of InviZible Pro.
 *     InviZible Pro is free software: you can redistribute it and/or modify
 *     it under the terms of the GNU General Public License as published by
 *     the Free Software Foundation, either version 3 of the License, or
 *     (at your option) any later version.
 *     InviZible Pro is distributed in the hope that it will be useful,
 *     but WITHOUT ANY WARRANTY; without even the implied warranty of
 *     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *     GNU General Public License for more details.
 *     You should have received a copy of the GNU General Public License
 *     along with InviZible Pro.  If not, see <http://www.gnu.org/licenses/>.
 *     Copyright 2019-2022 by Garmatin Oleksandr invizible.soft@gmail.com
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.gms.google-services'

def versionMajor = 5
def versionMinor = 5
def versionPatch = 7

android {
    compileSdkVersion 33

    flavorDimensions "version", "processor"

    productFlavors {

        fdroid {
            applicationId "com.fulldive.extension.tordnscrypt"
            versionName "${versionMajor}.${versionMinor}.${versionPatch}"
            dimension = 'version'
            resValue 'string', 'package_name', applicationId
        }

        armv7a {
            dimension = 'processor'
            resValue 'string', 'appProcVersion', 'armv7a'
            versionCode = 2

            ndk {
                abiFilters 'armeabi-v7a'
            }
        }

        arm64 {
            dimension = 'processor'
            resValue 'string', 'appProcVersion', 'arm64'
            versionCode = 3

            ndk {
                abiFilters 'arm64-v8a'
            }
        }

        universal {
            dimension = 'processor'
            resValue 'string', 'appProcVersion', 'universal'
            versionCode = 4

            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }
        }

    }

    signingConfigs {
        release {
            storeFile file('../keys/keys.jks')
            storePassword System.getenv("FULLDIVE_KEYSTORE_PASSWORD")
            keyAlias System.getenv("FULLDIVE_ALIAS")
            keyPassword System.getenv("FULLDIVE_ALIAS_PASSWORD")
        }
    }

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 33
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch

        resConfigs "en", "ru-rRU", "ru-rUA", "pl", "de", "fa", "fi", "in", "fr", "ja", "zh", "es", "pt-rBR"
        buildConfigField 'String', 'FLURRY_API_KEY', new File('./flurrykey.txt').getText('UTF-8')
        externalNativeBuild {
            cmake {
                cppFlags ""
                arguments "-DANDROID_PLATFORM=android-19"
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + System.currentTimeMillis() + "L)"

        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    output.outputFileName = "Hide-v${android.defaultConfig.versionCode}-${variant.productFlavors.name[0]}-${variant.buildType.name}.apk"
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    buildToolsVersion = '30.0.3'

    bundle {
        language {
            enableSplit = false
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs 'libs/'
        }
    }

    packagingOptions {
        doNotStrip '*/*/libdnscrypt-proxy.so'
        doNotStrip '*/*/libi2pd.so'
        doNotStrip '*/*/libobfs4proxy.so'
        doNotStrip '*/*/libsnowflake.so'
        doNotStrip '*/*/libtor.so'
    }

    applicationVariants.all { variant ->

        def abiVersion = variant.mergedFlavor.versionCode

        variant.outputs.each { output ->
            output.versionCodeOverride = abiVersion * 1000 + defaultConfig.versionCode
        }
    }

    ndkVersion '21.4.7075529'

    buildFeatures {
        viewBinding true
    }

    kotlinOptions.useIR = true

}

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.fulldive:Google-IAP:1.0.1"
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    //noinspection GradleDynamicVersion
    implementation 'eu.chainfire:libsuperuser:1.1.0.+'
    implementation 'com.jrummyapps:android-shell:1.0.1'
    implementation "androidx.core:core-ktx:1.7.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'com.github.angads25:filepicker:1.1.1'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.2"

    implementation("com.squareup.okhttp3:okhttp:4.9.1")
    implementation "com.airbnb.android:lottie:5.2.0"
    implementation 'com.android.billingclient:billing-ktx:5.0.0'

    implementation 'com.flurry.android:analytics:13.3.0'
    implementation 'com.google.android.gms:play-services-ads-identifier:18.0.1'
    implementation 'com.google.android.gms:play-services-measurement-api:21.0.0'
    implementation 'com.google.firebase:firebase-analytics:21.0.0'

}
